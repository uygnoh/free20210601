############################################################
### % ARM-CM3-GCC ( OS_CPU_PendSVHandler )
############################################################
OS_CPU_PendSVHandler
        CPSID   I                               ; Prevent interruption during context switch
        MRS     R0, PSP                         ; PSP is process stack pointer
        CBZ     R0, OS_CPU_PendSVHandler_nosave ; Skip register save the first time
        
        SUBS    R0, R0, #0x20                   ; R0 = R0+16
        STM     R0, {R4-R11}                    ; Save remaining regs r4-r11 on process stack
        
        LDR     R1, =OSTCBCur                   ; OSTCBCur->OSTCBStkPer = SP
        LDR     R1, [R1]
        STR     R0, [R1]                        ; R0 is Sp of process beging switched out


OS_CPU_PendSVHandler_nosave                     ; At this point, entire context of process has been saved
        PUSH    {R14}                           ; Save LR exc_return value
        LDR     R0, =OSTaskSwHook               ; OStaskSwHook
        BLX     R0
        POP     {R14}
        
        LDR     R0, =OSPrioCur                  ; OSPrioCur = OSPrioHighRdy
        LDR     R1, =OSPrioHighRdy
        LDRB    R2, [R1]
        STRB    R2, [R0];
        
        LDR     R0, =OSTCBCur                   ; OSTCBCur = OSTCBHighRdy
        LDR     R1, =OSTCBHighRdy
        LDR     R2, [R1]
        STR     R2, [R0]
        
        LDR     R0, [R2]
        LDM     R0, {R4-R11}
        ADDS    R0, R0, #0x20
        MSR     PSP, R0
        ORR     LR, LR, #0x04
        CPSIE   I
        BX      LR
        
        END
